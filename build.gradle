// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.library' version '8.9.3' apply false
    id 'org.jetbrains.kotlin.android' version '2.0.21' apply false
    id 'org.jetbrains.kotlin.multiplatform' version '2.0.21' apply false
    id 'com.android.kotlin.multiplatform.library' version '8.9.3' apply false
    id 'com.android.lint' version '8.9.3' apply false
}

ext {
    groupId = 'com.trackingplan.client'
    TrackingplanVersion = "3.0.0"
    TrackingplanAdapterVersion = "2.1.2"
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register('cleanBuildPublishLocal', GradleBuild) {
    tasks = ['clean', 'buildPublishLocal']
}

tasks.register('buildPublishLocal', GradleBuild) {
    tasks = ['build', 'publishLocal']
}

tasks.register('publishLocal', GradleBuild) {
    tasks = [
            'adapter:publishToMavenLocal',
            'sdk:publishToMavenLocal',
            'junit-tools:publishToMavenLocal',
            'shared:publishAndroidPublicationToMavenLocal',
            'shared:publishKotlinMultiplatformPublicationToMavenLocal'
    ]
}

tasks.register('cleanBuildPublishSonatype', GradleBuild) {
    tasks = [
            'clean',
            'buildPublishSonatype'
    ]
}

tasks.register('buildPublishSonatype', GradleBuild) {
    tasks = ['build', 'publishSonatype']
}

tasks.register('publishSdkSonatype', GradleBuild) {
    tasks = [
            'sdk:publishAllPublicationsToSonatypeRepository',
            'junit-tools:publishAllPublicationsToSonatypeRepository',
            'shared:publishAndroidPublicationToSonatypeRepository',
            'shared:publishKotlinMultiplatformPublicationToSonatypeRepository'
    ]
}

tasks.register('publishAdapterSonatype', GradleBuild) {
    tasks = ['adapter:publishAllPublicationsToSonatypeRepository']
}

tasks.register('publishSonatype', GradleBuild) {
    tasks = ['publishSdkSonatype', 'publishAdapterSonatype']
}

// Load local.properties to ext
File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
}
