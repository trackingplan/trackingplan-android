plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group rootProject.ext.groupId
version "${rootProject.ext.TrackingplanVersion}"

android {
    namespace "com.trackingplan.client.sdk"
    compileSdk 33

    defaultConfig {
        aarMetadata {
            minCompileSdk = 24
        }
        minSdk 24
        targetSdk 33
        versionCode 54
        versionName TrackingplanVersion
        buildConfigField 'String', 'SDK_VERSION', "\"${rootProject.ext.TrackingplanVersion}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        consumerProguardFiles "consumer-rules.pro"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    // Appcompat 1.6.1 requires API level 33 and depends on lifecycle-process 2.5.1.
    // Trackingplan SDK uses lifecycle-process as well so let's use the same version as Appcompat.
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.startup:startup-runtime:1.1.1"
    implementation "androidx.lifecycle:lifecycle-process:2.5.1"


    compileOnly "com.google.firebase:firebase-analytics:19.0.1"
    compileOnly "com.squareup.okhttp3:okhttp:3.14.9"
    compileOnly 'com.google.android.gms:play-services-tagmanager-v4-impl:18.0.2'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation('com.adevinta.android:barista:4.3.0')
}


publishing {
    publications {
        sdk(MavenPublication) {

            artifactId = 'sdk'

            pom {
                name = "Trackingplan Android SDK"
                description = "Android library to integrate your Android App with Trackingplan"
                url = "https://www.trackingplan.com"
                packaging = "aar"
                licenses {
                    license {
                        name = "Copyright Trackingplan 2022 - All rights reserved"
                    }
                }
                developers {
                    developer {
                        organization = "Trackingplan"
                        organizationUrl = "https://www.trackingplan.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/trackingplan/trackingplan-android.git'
                    developerConnection = 'scm:git:ssh://github.com/trackingplan/trackingplan-android.git'
                    url = 'https://github.com/trackingplan/trackingplan-android/tree/main'
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}