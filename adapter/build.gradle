plugins {
    id 'java-gradle-plugin'
    // id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
}

group rootProject.ext.groupId
version "${rootProject.ext.TrackingplanVersion}"

gradlePlugin {
    plugins {
        TrackingplanAdapterPlugin {
            id = 'com.trackingplan.client'
            implementationClass = 'com.trackingplan.client.adapter.TrackingplanPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

//compileKotlin {
//    kotlinOptions.jvmTarget = JavaVersion.VERSION_11
//}

dependencies {
    api 'com.android.tools.build:gradle:7.2.1'
    // These are transitive dependencies from Android Gradle Plugin. The same versions are used like
    // in gradle:7.2.1. Note that using a different number version might create classpath-related
    // build problems in projects using Trackingplan
    compileOnly 'com.android.tools:sdk-common:30.2.1'
    compileOnly 'com.android.tools:common:30.2.1'
    compileOnly 'com.google.guava:guava:30.1-jre'
    compileOnly 'commons-io:commons-io:2.4'
    implementation 'org.ow2.asm:asm:9.1'
    implementation 'org.ow2.asm:asm-commons:9.1'
}


publishing {
    publications {
        adapter(MavenPublication) {

            pom {
                name = "Trackingplan Android Adapter"
                description = "Gradle plugin for Trackingplan Android SDK"
                url = "https://www.trackingplan.com"
                packaging = "jar"
                licenses {
                    license {
                        name = "Copyright Trackingplan 2022 - All rights reserved"
                    }
                }
                developers {
                    developer {
                        organization = "Trackingplan"
                        organizationUrl = "https://www.trackingplan.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/trackingplan/trackingplan-android.git'
                    developerConnection = 'scm:git:ssh://github.com/trackingplan/trackingplan-android.git'
                    url = 'https://github.com/trackingplan/trackingplan-android/tree/main'
                }
            }

            from components.java
        }
    }
    repositories {
        maven {
            name = "sonatype"

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}


signing {
    sign publishing.publications
}