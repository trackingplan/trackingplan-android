plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
    id 'signing'
}

group rootProject.ext.groupId
version "${rootProject.ext.TrackingplanVersion}"

gradlePlugin {
    plugins {
        TrackingplanAdapterPlugin {
            id = 'com.trackingplan.client'
            implementationClass = 'com.trackingplan.client.adapter.TrackingplanPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.0.21"
    compileOnly gradleApi()
    compileOnly 'com.android.tools.build:gradle:8.0.2'
    // These are transitive dependencies from Android Gradle Plugin. The same versions are used like
    // in gradle:8.0.2. Note that using a different number version might create classpath-related
    // build problems in projects using Trackingplan
    compileOnly 'com.android.tools:sdk-common:31.0.2'
    compileOnly 'com.android.tools:common:31.0.2'
    compileOnly 'commons-io:commons-io:2.4'
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'
}


publishing {
    // afterEvaluate is necessary because java-gradle-plugin
    // creates its publications in an afterEvaluate callback
    afterEvaluate {
        publications {
            withType(MavenPublication) {
                pom {
                    url = "https://www.trackingplan.com"
                    licenses {
                        license {
                            name = "Copyright Trackingplan 2022 - All rights reserved"
                        }
                    }
                    developers {
                        developer {
                            organization = "Trackingplan"
                            organizationUrl = "https://www.trackingplan.com"
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/trackingplan/trackingplan-android.git'
                        developerConnection = 'scm:git:ssh://github.com/trackingplan/trackingplan-android.git'
                        url = 'https://github.com/trackingplan/trackingplan-android/tree/main'
                    }
                }
            }
            pluginMaven {
                pom {
                    name = "Trackingplan Android Adapter"
                    description = "Gradle plugin for Trackingplan Android SDK"
                    packaging = "jar"
                }
            }
            TrackingplanAdapterPluginPluginMarkerMaven {
                pom {
                    name = "Trackingplan Android Plugin"
                    description = "Gradle plugin for Trackingplan Android SDK"
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"

            def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}


signing {
    sign publishing.publications
}

afterEvaluate {
    tasks.withType(AbstractPublishToMaven) {
        dependsOn tasks.withType(Sign)
    }
}
